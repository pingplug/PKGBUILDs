# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

pkgname=mingw-w64-gcc
pkgver=9.1.0
#_pkgver=8.2.1+20181127
#_snapshot=9-20181125
_islver=0.21
_gmpver=6.1.2
_mpfrver=4.0.2
_mpcver=1.1.0
pkgrel=1
pkgdesc="Cross GCC for the MinGW-w64 cross-compiler"
arch=('x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=('zlib' 'libmpc'
	 'mingw-w64-crt' 'mingw-w64-binutils' 'mingw-w64-winpthreads'
	 'mingw-w64-headers')
makedepends=('gcc-ada' 'mingw-w64-gcc-base')
optdepends=()
provides=('mingw-w64-gcc-base')
replaces=('mingw-w64-gcc-base')
conflicts=('mingw-w64-gcc-base')
backup=()
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=(#"https://sources.archlinux.org/other/gcc/gcc-${_pkgver/+/-}.tar.xz"{,.sig}
        "https://mirrors.tuna.tsinghua.edu.cn/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.xz"{,.sig}
        #"https://gcc.gnu.org/pub/gcc/snapshots/$_snapshot/gcc-$_snapshot.tar.xz"
	    "http://isl.gforge.inria.fr/isl-${_islver}.tar.xz"
        "https://mirrors.tuna.tsinghua.edu.cn/gnu/gmp/gmp-${_gmpver}.tar.xz"{,.sig}
        "https://mirrors.tuna.tsinghua.edu.cn/gnu/mpfr/mpfr-${_mpfrver}.tar.xz"{,.sig}
        "https://mirrors.tuna.tsinghua.edu.cn/gnu/mpc/mpc-${_mpcver}.tar.gz"{,.sig})
validpgpkeys=('F3691687D867B81B51CE07D9BBE43771487328A9' # bpiotrowski@archlinux.org
              '33C235A34C46AA3FFB293709A328C3A2C3C45C06' # Jakub Jelinek <jakub@redhat.com>
              'AD17A21EF8AED8F1CC02DBD9F7D5C9BF765C61E3' # andreas@enge.fr
              '07F3DBBECC1A39605078094D980C197698C3739D' # vincent@vinc17.net
              '343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # nisse@lysator.liu.se

sha256sums=('79a66834e96a6050d8fe78db2c3b32fb285b230b855d0a66288235bc04b327a0'
            'SKIP'
            '777058852a3db9500954361e294881214f6ecd4b594c00da5eee974cd6a54960'
            '87b565e89a9a684fe4ebeeddb8399dce2599f9c9049854ca8c0dfbdea0e21912'
            'SKIP'
            '1d3be708604eae0e42d578ba93b390c2a145f17743a744d8f3f8c2ad5855a38a'
            'SKIP'
            '6985c538143c1208dcb1ac42cedad6ff52e267b47e5f970183a3e75125b43c2e'
            'SKIP')

pkgver() {
  cd "${srcdir}/gcc/gcc"
  cat BASE-VER
}

prepare() {
  #mv gcc-${_pkgver/+/-} gcc
  mv gcc-${pkgver} gcc
  #mv gcc-${_snapshot} gcc
  cd "$srcdir"/gcc
  # link isl for in-tree builds
  ln -sf ../isl-${_islver} isl
  # link gmp for in-tree builds
  ln -sf ../gmp-${_gmpver} gmp
  # link mpfr for in-tree builds
  ln -sf ../mpfr-${_mpfrver} mpfr
  # link mpc for in-tree builds
  ln -sf ../mpc-${_mpcver} mpc
}

build() {
  for _target in ${_targets}; do
    msg2 "Building ${_target}"
    mkdir -p "$srcdir"/gcc-build-${_target} && cd "$srcdir"/gcc-build-${_target}

    "$srcdir"/gcc/configure \
        --prefix=/usr \
        --libexecdir=/usr/lib \
        --target=${_target} \
        --disable-multilib \
        --enable-languages=c,c++,ada,fortran,lto,objc,obj-c++ \
        --enable-shared \
        --enable-static \
        --enable-threads=posix \
        --enable-fully-dynamic-string \
        --enable-libstdcxx-time=yes \
        --with-system-zlib \
        --enable-cloog-backend=isl \
        --enable-lto \
        --disable-sjlj-exceptions \
        --enable-plugin \
        --enable-libgomp \
        --enable-checking=release
    make all
  done
}

package() {
  for _target in ${_targets}; do
    msg2 "Installing ${_target}"
    cd "$srcdir"/gcc-build-${_target}
    make -j1 DESTDIR="$pkgdir" install
    ${_target}-strip "$pkgdir"/usr/${_target}/lib/*.dll
    strip "$pkgdir"/usr/bin/${_target}-*
    strip "$pkgdir"/usr/lib/gcc/${_target}/${pkgver}/{cc1*,collect2,gnat1,f951,lto*}
    ln -s ${_target}-gcc "$pkgdir"/usr/bin/${_target}-cc
    # mv dlls
    mkdir -p "$pkgdir"/usr/${_target}/bin/
    mv "$pkgdir"/usr/${_target}/lib/*.dll "$pkgdir"/usr/${_target}/bin/
  done
  strip "$pkgdir"/usr/bin/*
  # remove unnecessary files
  rm -r "$pkgdir"/usr/share
  rm -f "$pkgdir"/usr/lib/libcc1.so*
  rm -f "$pkgdir"/usr/lib/libcc1.a
}
