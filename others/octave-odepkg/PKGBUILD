# Maintainer: Joey Dumont <joey.dumont@gmail.com>
# Contributor: Airon90

_pack=odepkg
pkgname=octave-$_pack
pkgver=0.9.1
pkgrel=1
pkgdesc="A package for solving ordinary differential equations and more."
arch=(any)
url="http://octave.sourceforge.net/$_pack/"
license=('GPLv2+')
groups=('octave-forge')
depends=('octave>=3.8.0')
makedepends=('gcc-fortran')
install=$pkgname.install
_archive=$_pack-$pkgver.tar.gz
source=("$_pack-$pkgver.tar.gz::https://bitbucket.org/odepkg/odepkg/get/default.tar.gz")
noextract=("$_archive")
sha512sums=('586712db69b200a43fdd15248a7327fa2b504095ecdc7116e9969e44a2603dace82e937f1ed880fb3906314be39dba97c9d5ac75d5584d2b189c519ad736670a')

_octave_run() {
	octave --no-history --no-init-file --no-window-system -q -f --eval "$*"
}

_install_dir() {
	src=$1
	dst=$2
	mkdir -p "$(dirname "$dst")"
	cp -rT "$src" "$dst"
}

build() {
	_prefix="$srcdir"/install_prefix
	_archprefix="$srcdir"/install_archprefix
	mkdir -p "$_prefix" "$_archprefix"
	cd "$srcdir"
	_octave_run "$(cat <<-EOF
		pkg local_list octave_packages;
		pkg prefix $_prefix $_archprefix;
		pkg install -verbose -nodeps $_archive;
		EOF
		)"
}

package() {
	prefix=$pkgdir/usr/share/octave/packages
	archprefix=$pkgdir/usr/lib/octave/packages
	_install_dir "$srcdir"/install_prefix "$prefix"
	_install_dir "$srcdir"/install_archprefix "$archprefix"
}
