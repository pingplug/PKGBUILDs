# Maintainer: pingplug < aur at pingplug dot me >
# Contributor: Johannes Löthberg < johannes at kyriasis dot com >
# Contributor: Alexander F Rødseth < xyproto at archlinux dot org >
# Contributor: Daniel Micay < danielmicay at gmail dot com >
# Contributor: userwithuid < userwithuid at gmail dot com >

_pkgname=rust
_date=2020-08-27
_rustc=1.46.0
_cargo=0.47.0

_cargo_ver=0.48.0
_rustfmt_ver=1.4.20
_prefix=usr

pkgname=pingplug-rust
pkgver=1.47.0
pkgrel=1
pkgdesc="Systems programming language focused on safety, speed and concurrency (pingplug)"
arch=('x86_64')
url="https://www.rust-lang.org"
license=('MIT' 'Apache' 'custom')
depends=('gcc-libs'
         'lib32-gcc-libs'
         'curl'
         'libgit2'
         'mingw-w64-gcc'
         'mingw-w64-wine')
provides=("rust=${pkgver}" 
          "lib32-rust=${pkgver}" 
          "cargo=${_cargo_ver}"
          "rustfmt=${_rustfmt_ver}"
          "rust-docs=${pkgver}" 
          "mingw-w64-rust=${pkgver}")
conflicts=("rust" 
           "lib32-rust" 
           "cargo"
           "rustfmt"
           "rust-docs" 
           "mingw-w64-rust")
makedepends=('gdb'
             'libffi'
             'llvm'
             'perl'
             'python'
             'nodejs'
             'procps-ng'
             'cmake')
options=('!strip' 'staticlibs' '!buildflags')
source=("https://static.rust-lang.org/dist/rustc-${pkgver}-src.tar.xz"{,.asc}
        "https://static.rust-lang.org/dist/${_date}/rust-std-${_rustc}-x86_64-unknown-linux-gnu.tar.xz"{,.asc}
        "https://static.rust-lang.org/dist/${_date}/rustc-${_rustc}-x86_64-unknown-linux-gnu.tar.xz"{,.asc}
        "https://static.rust-lang.org/dist/${_date}/cargo-${_cargo}-x86_64-unknown-linux-gnu.tar.xz"{,.asc}
        "pingplug-config.toml")
noextract=("rust-std-${_rustc}-x86_64-unknown-linux-gnu.tar.xz"
           "rustc-${_rustc}-x86_64-unknown-linux-gnu.tar.xz"
           "cargo-${_cargo}-x86_64-unknown-linux-gnu.tar.xz")
sha256sums=('ec2c81d2d34890486094a6407589be96161e4e301c238332d32c6dbae4f38ea2'
            'SKIP'
            'e631d80cb03539769c041ee4566e94e36a271d4b3cdd149e1447d1f77fda979c'
            'SKIP'
            '6edcec5367f9fcaee78cbcabfb1f6757fa95d7fd2c0853913223fe20ad534f12'
            'SKIP'
            '9da5c4888c6025fa174eaef330337e49666ca9562f876a34d40c03a80d432b8d'
            'SKIP'
            'f8d2ba90f92eb5a26f827901dc19182d7cbafd3fcebfaa2cb24c1d333139a4c7')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

backup=("opt/rust/cargo/config")

prepare() {
  cd "rustc-${pkgver}-src"

  cp "${srcdir}"/pingplug-config.toml config.toml
  sed -i "s|\@PREFIX\@|/${_prefix}|" config.toml
  # use level 0 to speed up xz packaging
  sed -i 's|.preset(6)|.preset(0)|' "src/tools/rust-installer/src/tarballer.rs"

  cd "${srcdir}"
  mkdir -p "${srcdir}/rustc-${pkgver}-src/build/cache/${_date}"
  cp rust-std-${_rustc}-x86_64-unknown-linux-gnu.tar.xz "rustc-${pkgver}-src/build/cache/${_date}"
  cp rustc-${_rustc}-x86_64-unknown-linux-gnu.tar.xz "rustc-${pkgver}-src/build/cache/${_date}"
  cp cargo-${_cargo}-x86_64-unknown-linux-gnu.tar.xz "rustc-${pkgver}-src/build/cache/${_date}"
}

build() {
  cd "rustc-${pkgver}-src"

  export CFLAGS="-O2 -pipe -fno-plt -Wall -D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="-O2 -pipe -fno-plt -Wall -D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  python ./x.py build
}

package() {
  cd "rustc-${pkgver}-src"

  # rust will build install tools there
  export CFLAGS="-O2 -pipe -fno-plt -Wall -D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="-O2 -pipe -fno-plt -Wall -D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  # TODO: find a way to disable packaging
  DESTDIR="${pkgdir}" python ./x.py install --keep-stage 0 --keep-stage 1

  # license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"{rust,cargo}
  mv "${pkgdir}"/${_prefix}/share/doc/rust/LICENSE-*.old "${pkgdir}/usr/share/licenses/${pkgname}/cargo/"
  rename '.old' '' "${pkgdir}/usr/share/licenses/${pkgname}/cargo/"*
  mv "${pkgdir}"/${_prefix}/share/doc/rust/{LICENSE-*,COPYRIGHT} "${pkgdir}/usr/share/licenses/${pkgname}/rust/"

  # move bash files
  install -dm755 "${pkgdir}/usr/share/bash-completion"
  mv "${pkgdir}/etc/bash_completion.d/" "${pkgdir}/usr/share/bash-completion/completions/"

  # remove unused files
  rm -r "${pkgdir}/etc/"
  rm "${pkgdir}/${_prefix}/lib/rustlib/"{manifest-*,install.log,uninstall.sh,components,rust-installer-version}

  # link shared libraries
  install -dm755 "${pkgdir}/usr/lib32" && pushd "${pkgdir}/usr/lib32"
  ln -sf "../../${_prefix}/lib/rustlib/i686-unknown-linux-gnu/lib/"*.so .
  popd
  pushd "${pkgdir}/${_prefix}/lib"
  ln -sf "rustlib/x86_64-unknown-linux-gnu/lib/"*.so .
  popd
  install -dm755 "${pkgdir}/usr/i686-w64-mingw32/bin" && pushd "${pkgdir}/usr/i686-w64-mingw32/bin"
  ln -sf "../../../${_prefix}/lib/rustlib/i686-pc-windows-gnu/lib/"*.dll .
  popd
  install -dm755 "${pkgdir}/usr/x86_64-w64-mingw32/bin" && pushd "${pkgdir}/usr/x86_64-w64-mingw32/bin"
  ln -sf "../../../${_prefix}/lib/rustlib/x86_64-pc-windows-gnu/lib/"*.dll .
  popd

  # strip
  strip --strip-all "${pkgdir}/${_prefix}/bin/"{cargo,cargo-clippy,cargo-fmt,clippy-driver,rls,rustc,rustdoc,rustfmt}
  strip --strip-unneeded "${pkgdir}/${_prefix}/lib/librustc"*.so
  strip --strip-unneeded "${pkgdir}/${_prefix}/lib/rustlib/i686-unknown-linux-gnu/lib/"*.so
  strip --strip-unneeded "${pkgdir}/${_prefix}/lib/rustlib/x86_64-unknown-linux-gnu/lib/"*.so
  i686-w64-mingw32-strip --strip-unneeded "${pkgdir}/${_prefix}/lib/rustlib/i686-pc-windows-gnu/lib/"*.dll
  x86_64-w64-mingw32-strip --strip-unneeded "${pkgdir}/${_prefix}/lib/rustlib/x86_64-pc-windows-gnu/lib/"*.dll

  # change analyze
  sed -i "s|${srcdir}/rustc-${pkgver}-src/src|/${_prefix}/lib/rustlib/src/rust/src|g" "${pkgdir}/${_prefix}/lib/rustlib/"*"/analysis/"*.json
  sed -i "s|${srcdir}/rustc-${pkgver}-src/build|/BUILD_DIR|g" "${pkgdir}/${_prefix}/lib/rustlib/"*"/analysis/"*.json
  sed -i "s|${srcdir}/rustc-${pkgver}-src|/COMPILATION_DIR|g" "${pkgdir}/${_prefix}/lib/rustlib/"*"/analysis/"*.json

  # config
  install -dm777 "${pkgdir}/opt/rust/cargo"
  cat << EOF >> "${pkgdir}/opt/rust/cargo/config"
[source.crates-io]
replace-with = 'tuna'

[source.tuna]
registry = "https://mirrors.tuna.tsinghua.edu.cn/git/crates.io-index.git"

EOF
  cat << EOF >> "${pkgdir}/opt/rust/cargo/config"
[target.i686-pc-windows-gnu]
linker = "/usr/bin/i686-w64-mingw32-gcc"
ar = "/usr/i686-w64-mingw32/bin/ar"
EOF
  if pacman -T "mingw-w64-wine" ; then
    cat << EOF >> "${pkgdir}/opt/rust/cargo/config"
runner = "/usr/bin/i686-w64-mingw32-wine"
EOF
  fi
  cat << EOF >> "${pkgdir}/opt/rust/cargo/config"
rustflags = [
            ]

EOF

  cat << EOF >> "${pkgdir}/opt/rust/cargo/config"
[target.x86_64-pc-windows-gnu]
linker = "/usr/bin/x86_64-w64-mingw32-gcc"
ar = "/usr/x86_64-w64-mingw32/bin/ar"
EOF
  if pacman -T "mingw-w64-wine" ; then
    cat << EOF >> "${pkgdir}/opt/rust/cargo/config"
runner = "/usr/bin/x86_64-w64-mingw32-wine"
EOF
  fi
  cat << EOF >> "${pkgdir}/opt/rust/cargo/config"
rustflags = [
            ]

EOF
}

# vim:set ts=2 sw=2 et:
