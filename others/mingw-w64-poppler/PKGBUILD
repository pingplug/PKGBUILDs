# Maintainer: ant32 <antreimer@gmail.com>
pkgname=mingw-w64-poppler
pkgver=0.63.0
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0 (mingw-w64)"
arch=(any)
url="http://poppler.freedesktop.org"
license=("GPL")
makedepends=(mingw-w64-configure mingw-w64-glib2 mingw-w64-icu mingw-w64-curl)
depends=(mingw-w64-openjpeg2 mingw-w64-cairo)
optdepends=("mingw-w64-glib2: libpoppler-glib")
options=(!strip !buildflags staticlibs)
source=("http://poppler.freedesktop.org/poppler-${pkgver}.tar.xz"
        "poppler.patch")
sha256sums=('27cc8addafc791e1a26ce6acc2b490926ea73a4f89196dd8a7742cff7cf8a111'
            '47f7d9ed0fa1c418b1062f5cb0e3ae5578c40b6529f965566f023711b2fd0954')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir"

  # fix a typo
  # FIXME: dirty hack! will cause memory leak
  patch -p0 -i poppler.patch
}

build() {
  cd "$srcdir/poppler-$pkgver"
  unset LDFLAGS

  for _arch in ${_architectures}; do
    mkdir -p "${_arch%%-*}" && pushd "${_arch%%-*}"
    ${_arch}-cmake \
        -DBUILD_CPP_TESTS=NO \
        -DBUILD_GTK_TESTS=NO \
        -DBUILD_QT5_TESTS=NO \
        ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/poppler-$pkgver/${_arch%%-*}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -exec ${_arch}-strip {} \;
    find "$pkgdir/usr/${_arch}" -name '*.dll' -exec ${_arch}-strip --strip-unneeded {} \;
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs ${_arch}-strip -g
  done
}
