# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

pkgname=mingw-w64-gcc-base
pkgver=9.3.0
#_pkgver=9.1.1+20190801
#_snapshot=9-20190810
_islver=0.22.1
_gmpver=6.2.0
_mpfrver=4.0.2
_mpcver=1.1.0
pkgrel=1
pkgdesc="Cross GCC for the MinGW-w64 cross-compiler, bootstrap"
arch=('x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=('zlib' 'mingw-w64-binutils' 'mingw-w64-headers')
makedepends=('gcc-ada')
optdepends=()
provides=()
replaces=()
backup=()
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=(#"https://sources.archlinux.org/other/gcc/gcc-${_pkgver/+/-}.tar.xz"{,.sig}
        "https://mirrors.tuna.tsinghua.edu.cn/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.xz"{,.sig}
        #"https://gcc.gnu.org/pub/gcc/snapshots/$_snapshot/gcc-$_snapshot.tar.xz"
	    "http://isl.gforge.inria.fr/isl-${_islver}.tar.xz"
        "https://mirrors.tuna.tsinghua.edu.cn/gnu/gmp/gmp-${_gmpver}.tar.xz"{,.sig}
        "https://mirrors.tuna.tsinghua.edu.cn/gnu/mpfr/mpfr-${_mpfrver}.tar.xz"{,.sig}
        "https://mirrors.tuna.tsinghua.edu.cn/gnu/mpc/mpc-${_mpcver}.tar.gz"{,.sig})
validpgpkeys=('F3691687D867B81B51CE07D9BBE43771487328A9' # bpiotrowski@archlinux.org
              '33C235A34C46AA3FFB293709A328C3A2C3C45C06' # Jakub Jelinek <jakub@redhat.com>
              'AD17A21EF8AED8F1CC02DBD9F7D5C9BF765C61E3' # andreas@enge.fr
              '07F3DBBECC1A39605078094D980C197698C3739D' # vincent@vinc17.net
              '343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # nisse@lysator.liu.se

sha256sums=('71e197867611f6054aa1119b13a0c0abac12834765fe2d81f35ac57f84f742d1'
            'SKIP'
            '28658ce0f0bdb95b51fd2eb15df24211c53284f6ca2ac5e897acc3169e55b60f'
            '258e6cd51b3fbdfc185c716d55f82c08aff57df0c6fbd143cf6ed561267a1526'
            'SKIP'
            '1d3be708604eae0e42d578ba93b390c2a145f17743a744d8f3f8c2ad5855a38a'
            'SKIP'
            '6985c538143c1208dcb1ac42cedad6ff52e267b47e5f970183a3e75125b43c2e'
            'SKIP')

pkgver() {
  cd "${srcdir}/gcc/gcc"
  cat BASE-VER
}

prepare() {
  #mv gcc-${_pkgver/+/-} gcc
  mv gcc-${pkgver} gcc
  #mv gcc-${_snapshot} gcc
  cd "$srcdir"/gcc
  # link isl for in-tree builds
  ln -sf ../isl-${_islver} isl
  # link gmp for in-tree builds
  ln -sf ../gmp-${_gmpver} gmp
  # link mpfr for in-tree builds
  ln -sf ../mpfr-${_mpfrver} mpfr
  # link mpc for in-tree builds
  ln -sf ../mpc-${_mpcver} mpc
}

build() {
  for _target in ${_targets}; do
    mkdir -p "$srcdir"/gcc-build-${_target} && cd "$srcdir"/gcc-build-${_target}

    "$srcdir"/gcc/configure \
        --prefix=/usr \
        --libexecdir=/usr/lib \
        --target=${_target} \
        --disable-multilib \
        --enable-languages=c,c++ \
        --disable-shared \
        --enable-static \
        --disable-threads \
        --enable-fully-dynamic-string \
        --enable-libstdcxx-time=yes \
        --enable-libstdcxx-filesystem-ts=yes \
        --with-system-zlib \
        --enable-cloog-backend=isl \
        --enable-lto \
        --disable-sjlj-exceptions \
        --disable-plugin \
        --disable-libgomp \
        --enable-checking=release
    make all-gcc all-target-libgcc
  done
}

package() {
  for _target in ${_targets}; do
    cd "$srcdir"/gcc-build-${_target}
    make DESTDIR="$pkgdir" install-gcc install-target-libgcc
    strip "$pkgdir"/usr/bin/${_target}-*
    strip "$pkgdir"/usr/lib/gcc/${_target}/${pkgver}/{cc1*,collect2,lto*}
    ln -s ${_target}-gcc "$pkgdir"/usr/bin/${_target}-cc
  done
  strip "$pkgdir"/usr/bin/*
  # remove unnecessary files
  rm -r "$pkgdir"/usr/share
  rm -f "$pkgdir"/usr/lib/libcc1.*
}
